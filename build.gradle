buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
    }
}

plugins {
	id 'com.matthewprenger.cursegradle' version '1.0.8'
}

apply plugin: 'net.minecraftforge.gradle.forge'
sourceCompatibility = targetCompatibility = "1.8"

group = "xmrvizzy.simpledrills"
archivesBaseName = mod_name
version = mod_version

minecraft {
	version = forge_version
	mappings = mcp_mappings
	runDir = "run"
	
	replaceIn "SimpleDrills.java"
	replace "@VERSION@", mod_version
	replace "required-after:xvlib", "required-after:xvlib@[$xvlib_version,)"
}

dependencies {
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
	
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

//Jars
task sourcesJar(type: Jar, dependsOn: classes) {
	from sourceSets.main.allSource
	classifier = 'sources'
}

task deobfJar(type: Jar) {
	from sourceSets.main.output
	classifier = "deobf"
}

artifacts {
	archives deobfJar
	archives sourcesJar
}

task signJar(type: SignJar, dependsOn: reobfJar) {
	onlyIf {
		project.hasProperty('keyStore')
	}

	keyStore = project.keyStore
	alias = project.keyStoreAlias
	storePass = project.keyStorePass
	keyPass = project.keyStoreKeyPass
	inputFile = jar.archivePath
	outputFile = jar.archivePath
}

build.dependsOn signJar

//Curseforge API
ext.curse_key = System.getenv("CURSE_KEY")
tasks.curseforge.onlyIf { curse_key }

curseforge {
	apiKey = curse_key
	
	project {
		id = curse_id
		changelog = ""
		releaseType = curse_type
		addGameVersion minecraft.version
	
		mainArtifact(jar) {
			displayName = mod_name + "-" + mod_version
			relations {
				requiredLibrary 'xvlib'
			}
		}
		addArtifact(deobfJar)
		addArtifact(sourcesJar)
	}
}